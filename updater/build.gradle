import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
  id('java')
  id('application')
  alias(libs.plugins.shadow)
  alias(libs.plugins.spotless)
}

application {
  mainClass = 'Main'
}

spotless {
  java {
    removeUnusedImports()
    googleJavaFormat()
  }
}

dependencies {
  implementation(libs.retrofit.core)
  implementation(libs.retrofit.moshi)
  implementation(libs.okhttp)
  implementation(libs.moshi)
}

tasks.named('shadowJar', ShadowJar) {
  duplicatesStrategy = DuplicatesStrategy.EXCLUDE
  enableAutoRelocation = true
  failOnDuplicateEntries = true
  minimize()
}

tasks.named('run', JavaExec) {
  // Pass the outside of the project directory.
  args '..'
}

tasks.named('assemble') {
  dependsOn(tasks.named('installDist'))
}
